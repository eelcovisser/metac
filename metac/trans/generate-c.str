module generate-c

imports

  BaseC/utils/scope-variable-creator
  signatures/BaseC-sig
  pp

strategies

  generate01 = fail
  generate02 = fail
  generate03 = fail
  generate(|t) = fail
  generate = fail

  generate-include-headers = fail

  generate-includes:
    Program(x, decls)
    -> Program(x, <conc> (includes, decls))
    where
      includes := <collect-all(generate-include-headers)> decls

  generate-temp-scope-variable = fail
  generate-temp-scope-variables =
    create-scope-variables(generate-temp-scope-variable)

  generate-pp =
    pp-debug // try to pretty print
    <+ debug // or print the AST

  generate-c: t@(selected, position, ast, path, project-path) -> (file-name, code)
    where
      file-name := <guarantee-extension(|"c")> path
      ; code := <generate-c-ast(|t) ; generate-pp> selected

  generate-c-ast(|t) =
    try(generate-includes)
    ; bottomup(try(generate-temp-scope-variables))
    ; bottomup(try(generate01)   ; try(flatten-list))
    ; bottomup(try(generate02)   ; try(flatten-list))
    ; bottomup(try(generate03)   ; try(flatten-list))
    ; bottomup(try(generate(|t)) ; try(flatten-list))
    ; bottomup(try(generate)     ; try(flatten-list))
