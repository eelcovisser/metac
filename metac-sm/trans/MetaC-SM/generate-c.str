module MetaC-SM/generate-c

imports

  libstratego-lib
  runtime/properties/-
  runtime/types/-
  runtime/nabl/-
  MetaC-SM/constructors
  names/MetaC-SM/names
  names/BaseC/names/variables
  BaseC/desugar/-
  BaseC/generate/functions
  signatures/MetaC-SM-sig
  signatures/BaseC-sig

rules

  generate = generate-sm

  generate-sm:
    sm@StateMachine(Identifier(name), InitState(Identifier(init)), decls, EventsList(events), states)
    -> <flatten-list> [
      <gen-sm-event-params-struct> (name, events),
      <gen-sm-struct> (name, decls),
      <gen-sm-transition-func> (name, states'),
      <gen-sm-init> (name, init, states', decls),
      <gen-sm-exec> (name, states', events)
    ]
    where
      states' := <gen-sm-local-vars> (states, sm)

  // struct

  gen-sm-events-have-params =
    map(?Event(_, _, EventParams(<id>)))
    ; concat
    ; ?[_ | _]

  gen-sm-event-params-struct: (name, events)
    -> Declaration(
      [Typedef(), Struct(None(), struct-declarations)],
      [Declarator(None(), Decl(Identifier(params-struct-name)))]
    )
    where
      <gen-sm-events-have-params> events
      ; params-struct-name := $[[name]_EventParams]
      ; all-params := <map(?Event(_, _, EventParams(<id>))) ; concat> events
      ; struct-declarations := <map(gen-sm-struct-decl-from-parameter)> all-params

  gen-sm-event-params-struct: (name, _) -> []

  gen-sm-struct-decl-from-parameter:
    FunDefinitionParameter(type, name)
    -> StructDeclaration(<gen-Type> type, [<generate-Declarator> (name, type)])

  // main state machine struct

  gen-sm-struct: (name, decls) ->
    Declaration(
      [Typedef(), Struct(None(), struct-declarations)],
      [Declarator(None(), Decl(Identifier(name)))]
    )
    where
      struct-declarations := <conc> (
        [StructDeclaration([Int()], [Declarator(None(), Decl(Identifier(<gen-sm-state-property>)))])],
        <map(gen-sm-variable)> decls
      )

  gen-sm-state-property = !"__sm_state__"
  gen-sm-event = !"__sm_event__"
  gen-sm-cont = !"__sm_cont__"

  gen-sm-variable:
    Declaration(type, [InitDeclarator(declarator, _)]) -> StructDeclaration(type, [declarator])

  gen-sm-variable:
    Declaration(type, declarator) -> StructDeclaration(type, declarator)

  // init

  gen-sm-init: (name, init, states, decls) ->
    FunDef(
      [Void()],
      FunDeclarator(
        None(),
        Identifier(<conc-strings> (sa-name, "_init")),
        ParameterList([
          Parameter(
            [TypedefName(Identifier(sa-name))],
            Declarator(Some(Pointer([], None())), Decl(Identifier("sm")))
          )
        ])
      ),
      [],
      CompoundStatement(<conc> (var-inits, [
        ExpressionStatement(Some(Call(
          Var(Identifier(<conc-strings> (sa-name, "_transition_to_state"))),
          [
            Var(Identifier("sm")),
            Integer(initial)
          ]
        )))
      ]))
    )
    where
      initial := <find-state-index> (init, states)
      ; sa-name := <strip-annos> name
      ; var-inits := <filter(gen-sm-var-init)> decls

  gen-sm-var-init: Declaration(type, [InitDeclarator(decl, val)])
    -> <gen-sm-assign-pointer-field> ("sm", name, val)
    where
      name := <find-decl ; ?Decl(Identifier(<id>))> decl

  gen-sm-assign-pointer-field: (var, prop, val) ->
    ExpressionStatement(Some(Assign(
     <gen-sm-pointer-field> (var, prop),
     Assign(),
     val
   )))

  gen-sm-pointer-field: (var, field) -> PointerField(Var(Identifier(var)), Identifier(field))

  get-state-name: State(Identifier(name), _, _) -> name

  find-state-index: (name, states) -> index
    where
      index := <add-indices
        ; fetch-elem(\(i, state) -> <strip-annos ; eq ; !<dec> i> (name, <get-state-name> state) \)
        ; int-to-string> states

  // exec function

  gen-sm-exec: (name, states, events) ->
    FunDef(
      [Void()],
      FunDeclarator(
        None(),
        Identifier(<conc-strings> (sa-name, "_exec")),
        ParameterList(<flatten-list> [
          // state machine instance
          Parameter(
            [TypedefName(Identifier(name))],
            Declarator(Some(Pointer([], None())), Decl(Identifier("sm")))
          ),
          // which event fired
          Parameter(
            [Int()],
            Declarator(None(), Decl(Identifier(<gen-sm-event>)))
          ),
          <(gen-sm-events-have-params ; !Parameter(
            [TypedefName(Identifier($[[name]_EventParams]))],
            Declarator(Some(Pointer([], None())), Decl(Identifier("event_args")))
          )) <+ ![]> events
        ])
      ),
      [],
      CompoundStatement(<concat> [
        local-event-params-vars,
        [Declaration(
          [Int()],
          [InitDeclarator(Declarator(None(), Decl(Identifier(<gen-sm-cont>))), Integer("1"))]
        )], // int cont = 1;
        local-event-params-assigns,
        [While(Var(Identifier(<gen-sm-cont>)), CompoundStatement([ // while (cont) {}
          // cont = 0;
          <gen-sm-assign-var> (<gen-sm-cont>, Integer("0")),
          Switch(
            // switch (sm->state) {}
            PointerField(Var(Identifier("sm")), Identifier(<gen-sm-state-property>)),
            CompoundStatement(cases)
          )
        ]))]
      ])
    )
    where
      sa-name := <strip-annos> name
      ; all-params := <map(?Event(_, _, EventParams(<id>))) ; concat> events
      ; local-event-params-vars := <
        map( \
          FunDefinitionParameter(type, name)
          -> Declaration(<gen-Type> type, [<generate-Declarator> (name, type)])
        \ )> all-params
      ; local-event-params-assigns := <
        map-with-index( \
          (i, Event(_, _, EventParams(params)))
          -> <?[] ; !None() <+ !Some(If(
            Equal(Var(Identifier(<gen-sm-event>)), Integer(<dec ; int-to-string> i)),
            CompoundStatement(<map( \
              FunDefinitionParameter(type, Decl(name))
              -> ExpressionStatement(Some(Assign(
                  Var(name),
                  Assign(),
                  PointerField(Var(Identifier("event_args")), name)
                )))
              \ )> params)
          ))> params
        \ )
        ; filter(?Some(<id>))> events
      ; let apply(|i) = <gen-sm-state(|name, states, events)> (i, <id>)
          in cases := <nmap(apply | 0)> states
        end

  gen-sm-assign-var: (var, val) ->
    ExpressionStatement(
      Some(Assign(Var(Identifier(var)), Assign(), val))
    )

  gen-sm-state(|name, states, events): (i, State(_, entry, trans)) ->
    Case(Integer(<int-to-string> i), CompoundStatement(
      <conc> (transitions, [Break()])
    ))
    where
      transitions := <map(gen-sm-transition(|name, states, events))> trans

  // epsilon transition
  gen-sm-transition(|name, states, events):
    Transition(None(), guard, effect, Identifier(new-state))
    -> <gen-sm-transition-internal> (name, predicate, effect, new-state-index)
    where
      predicate := <gen-sm-guard> guard
      ; new-state-index := <find-state-index> (new-state, states)

  // event action
  gen-sm-transition(|name, states, events):
    Transition(Some(Identifier(event-name)), guard, effect, Identifier(new-state))
    -> <gen-sm-transition-internal> (name, predicate, effect, new-state-index)
    where
      event-index := <get-event-index> event-name
      ; predicate := LogicalAnd(
          Equal(Var(Identifier(<gen-sm-event>)), Integer(<int-to-string> event-index)),
          <gen-sm-guard> guard
        )
      ; new-state-index := <find-state-index> (new-state, states)

  gen-sm-transition-internal: (name, predicate, effect, new-state-index)
    -> If(predicate, body)
    where
      sa-name := <strip-annos> name
      ; body := CompoundStatement(
          <conc> (
            <gen-sm-effect> effect,
            [
              <gen-sm-assign-var> (<gen-sm-cont>, Integer("1")),
              <gen-sm-assign-var> (<gen-sm-event>, Integer("-1")),
              ExpressionStatement(Some(Call(
                Var(Identifier(<conc-strings> (sa-name, "_transition_to_state"))),
                [Var(Identifier("sm")), Integer(new-state-index)]
              ))),
              Break()
            ]
          )
        )

  gen-sm-guard: None() -> Integer("1")
  gen-sm-guard: Guard(e) -> e

  gen-sm-effect: None() -> []
  gen-sm-effect: Effect(stmt) -> [stmt]

  // x_transition_to_state
  gen-sm-transition-func: (name, states) -> FunDef(
      [Void()],
      FunDeclarator(
        None(),
        Identifier(<conc-strings> (sa-name, "_transition_to_state")),
        ParameterList([
          Parameter(
            [TypedefName(Identifier(name))],
            Declarator(Some(Pointer([], None())), Decl(Identifier("sm")))
          ),
          Parameter([Int()], Declarator(None(), Decl(Identifier("goal"))))
        ])
      ),
      [],
      CompoundStatement(<concat> [
        <gen-sm-entries-switch> entries,
        [<gen-sm-assign-pointer-field> ("sm", <gen-sm-state-property>, Var(Identifier("goal")))]
      ])
    )
    where
      sa-name := <strip-annos> name
      ; entries := <add-indices
        ; filter(\ (i, state) -> (<dec> i, <?State(_, <id>, _) ; ?Entry(_)> state) \)
        > states

  gen-sm-entries-switch: [] -> []
  gen-sm-entries-switch: entries -> [
      Switch(
        Var(Identifier("goal")),
        CompoundStatement(<map(gen-sm-entry-case)> entries)
      )
    ]

  gen-sm-entry-case: (state-index, Entry(CompoundStatement(stmts))) ->
    Case(
      Integer(<int-to-string> state-index),
      CompoundStatement(<conc> (stmts, [Break()]))
    )

  gen-sm-local-vars: (ast, sm) -> <bottomup(try(gen-sm-local-var(|sm)))> ast

  gen-sm-local-var(|sm): var@Var(Identifier(name)) -> res
    where
      uri := <nabl-uri> name
      ; <nabl-uri-namespace-eq(|NablNsVariable())> uri
      ; parent-uri := <nabl-uri-parent> uri
      ; sm-uri := <nabl-get-name ; nabl-uri> sm
      ; if <nabl-uri-eq> (parent-uri, sm-uri) then
          res := <gen-sm-pointer-field> ("sm", name)
        else
          res := var
        end

  // call site

  generate: StateMachineVariable(name@Identifier(sm-name), var@Identifier(var-name))
    -> <flatten-list> [
      Declaration(
        [TypedefName(name)],
        [Declarator(None(), Decl(var))]
      ),
      <(gen-sm-events-have-params ; !Declaration(
        [TypedefName(Identifier($[[sa-name]_EventParams]))],
        [Declarator(None(), Decl(Identifier($[[var-name]_event_args])))]
      )) <+ ![]> events,
      ExpressionStatement(Some(
        Call(
          Var(Identifier($[[sa-name]_init])),
          [Address(Var(var))]
        )
      ))
    ]
    where
      sa-name := <strip-annos> sm-name
      ; events := <get-sm-events ; ?EventsList(<id>)> sm-name

  generate: StateMachineTriggerEvent(e@Var(Identifier(var-name)), event@Identifier(name), args)
    -> <?[] ; !expr <+ !Paren(Comma(expr, expressions))> expressions
    where
      sa-name := <get-type ; ?StateMachine(<id>)> e
      ; events := <get-sm-events ; ?EventsList(<id>)> sa-name
      ; params-var := Var(Identifier($[[var-name]_event_args]))
      ; index := <get-event-index> name
      ; params := <get-event-params ; ?EventParams(<id>)> name
      ; assignments := <map-with-index( \
          (i, FunDefinitionParameter(_, Decl(name)))
          -> Assign(Field(params-var,name), Assign(), <index> (i, args)
        ) \ )> params
      ; [expr | expressions] := <conc> (
          assignments,
          [Call(
            Var(Identifier(<conc-strings> (sa-name, "_exec"))),
            <conc> (
              [
                Address(e),
                Integer(<int-to-string> index)
              ],
              <(gen-sm-events-have-params ; ![Address(params-var)]) <+ ![]> events
            )
          )]
        )

  generate: StateMachineField(e, name) -> Field(e, name)
