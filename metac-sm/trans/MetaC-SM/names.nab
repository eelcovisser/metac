module MetaC-SM/names

imports

  BaseC/names/variables
  BaseC/names/functions
  MetaC-SM/-

namespaces

  StateMachine State Event

properties

  event-index of Event: EventIndex
  event-params of Event: EventParamsStart
  sm-events of StateMachine: StateMachineEvents

binding rules

  StateMachine(Identifier(name), _, _, events, _):
    defines StateMachine name
    of sm-events events
    scopes Variable, Parameter, Event, State

  InitState(Identifier(init)):
    refers to State init

  Event(index, Identifier(name), params):
    defines Event name
    of event-index index
    of event-params params

  State(Identifier(state), _, _):
    defines State state

  Transition(None(), _, _, Identifier(state)):
    refers to State state

  Transition(Some(Identifier(event)), _, _, Identifier(state)):
    refers to Event event
    refers to State state

  StateMachineVariable(Identifier(sm-name), Identifier(var-name)):
    refers to StateMachine sm-name
    defines Variable var-name of type StateMachine(sm-name)

  StateMachineTriggerEvent(e, Identifier(event), params):
    refers to Event event in StateMachine s
      where e has type StateMachine(s)

  StateMachineField(e, Identifier(var)):
    refers to Variable var in StateMachine s
      where e has type StateMachine(s)
